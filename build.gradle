plugins {
    id "org.jetbrains.intellij" version "0.0.42"
    id "de.undercouch.download" version "2.0.0"
    id 'nebula.provided-base' version '3.0.3' // if you want provided-base
}

apply plugin: 'idea'
apply plugin: 'org.jetbrains.intellij'
apply plugin: 'de.undercouch.download'
apply plugin: 'nebula.provided-base'

apply from: 'gradle/codegen.gradle'

repositories {
    mavenLocal()
    mavenCentral()
}

def javaVersion = JavaVersion.VERSION_1_8

sourceCompatibility = javaVersion
targetCompatibility = javaVersion

idea {
    project {
        jdkName = javaVersion
        languageLevel = javaVersion
    }
}

intellij {
    version = ideaVersion
    pluginName 'Frege'

    publish {
        username System.getenv('JETBRAINS_USER')
        password System.getenv('JETBRAINS_PASS')
        pluginId '8009'
        // channel 'nightly'
    }
}

dependencies {
    provided files("${System.properties['java.home']}/../lib/tools.jar")
    testCompile 'junit:junit:4.12'
    testCompile 'org.hamcrest:hamcrest-all:1.3'
}
sourceSets.main.java.srcDirs = ['src/main/java', 'gen', "$buildDir/generatedSources"]

import org.apache.tools.ant.filters.ReplaceTokens

processResources {
    filesMatching('**/plugin.xml') {
        filter ReplaceTokens, tokens: [
            pluginVersion: pluginVersion(projectVersion)
        ]
    }
}

def pluginVersion(baseVersion) {
    def gitCommand = "git rev-parse --short HEAD"
    def proc = gitCommand.execute()
    def revision = proc.text.trim()
    def timestamp = (int)(new Date().getTime()/1000)
    return baseVersion + '-' + revision + '-' + timestamp
}
