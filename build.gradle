plugins {
    id "org.jetbrains.intellij" version "0.0.42"
    id "de.undercouch.download" version "2.0.0"
    id 'nebula.provided-base' version '3.0.3' // if you want provided-base
}

repositories {
    mavenLocal()
    mavenCentral()
}

apply plugin: 'org.jetbrains.intellij'
apply plugin: 'de.undercouch.download'
apply plugin: 'nebula.provided-base'

apply from: 'gradle/codegen.gradle'

sourceCompatibility = 1.8
targetCompatibility = 1.8

intellij {
    version '15.0.4'
    pluginName 'Frege'

    publish {
        username System.getenv('JETBRAINS_USER')
        password System.getenv('JETBRAINS_PASS')
        pluginId '8009'
        // channel 'nightly'
    }
}

dependencies {
    provided files("${System.properties['java.home']}/../lib/tools.jar")
}
sourceSets.main.java.srcDirs = ['src/main/java', 'gen', "$buildDir/generatedSources"]

import org.apache.tools.ant.filters.ReplaceTokens

processResources {
    filter ReplaceTokens, tokens: [
        pluginVersion: pluginVersion(projectVersion)
    ]
}

def pluginVersion(baseVersion) {
    def gitCommand = "git rev-parse --short HEAD"
    def proc = gitCommand.execute()
    def revision = proc.text.trim()
    def timestamp = (int)(new Date().getTime()/1000)
    return baseVersion + '-' + revision + '-' + timestamp
}
